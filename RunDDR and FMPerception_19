# v1.1 copes will manage three states of FMPerception. Being closed, having no windows open, and just needs refresh
# v1.2 added wait when initially closed to allow windows to redraw
# v1.3 changed long wait for window to close to cope with running a DDR on a live file over t'interwebs
# v1.4 changed to point to key command to "refresh", rather than menu item index - future proofing from Dave
# v1.5 amended XML checkbox if version is FMP16 or above, fixes #1 thanks to HansReinJacobi
# v1.6 amended to alter behaviour for High Sierra and also for changes in FMP17¬
# v1.7 amended to alter behaviour for Mojave and also for changes in FMP18+ - removed Advanced, changes to FMP/ FMPerception objects


tell application "FileMaker Pro"
	set vv to version
	set fmv to text 1 thru 2 of vv as integer
end tell

set _vString to system version of (system info)

considering numeric strings
	set _newest to _vString ≥ "10.13"
	set _newer to _vString ≥ "10.14"
end considering

tell application "System Events"
	tell process "FileMaker Pro"
		#  export the DDR as XML, not automatically opened
		#  saves to last location with all files selected
		set frontmost to true
		try
			click menu item "Database Design Report..." of menu "Tools" of menu bar item 10 of menu bar 1
		on error
			try
				#  this will work if script workspace is in front
				click menu item "Database Design Report..." of menu "Tools" of menu bar item 7 of menu bar 1
			on error
				#  this will work if the DB is layout mode is in front
				click menu item "Database Design Report..." of menu "Tools" of menu bar item 11 of menu bar 1
			end try
		end try
		
		#  select XML as format
		if fmv > 15 then
			click radio button 2 of window "Database Design Report"
		else
			click checkbox 3 of window "Database Design Report"
		end if
		
		set theCheckbox to checkbox "Automatically open report when done" of window "Database Design Report"
		tell theCheckbox
			set checkboxStatus to value of theCheckbox as boolean
			#  turn off auto-open
			if checkboxStatus is true then click theCheckbox
		end tell
		#  Create
		click button 1 of window "Database Design Report"
		delay 0.1
		#  Save
		if _newer then
			click button 4 of window "Save Report"
		else if _newest then
			click button 3 of window "Save Report"
		else
			click button 1 of window "Save Report"
		end if
		try
			delay 0.5
			#  Save All
			if fmv > 18 then
				click button 2 of window "FileMaker Pro"
			else if fmv > 16 then
				click button 2 of window "FileMaker Pro Advanced"
			else
				click button 2 of window "FileMaker Pro"
			end if
			delay 0.5
			repeat until not window "Saving Report" exists
				#  you need this is if the export is from slow remote files, otherwise comment out
				delay 0.1
			end repeat
			
		on error
			#  first time save, not there so move on
		end try
	end tell
	set refreshed to false
	try
		tell process "FMPerception"
			set frontmost to true
			try
				set this_window to name of window 1
				#  exits if there is no window open
				#  File:Refresh
				# _dep click menu item 5 of menu 1 of menu bar item 3 of menu bar 1
				keystroke "r" using command down
				set refreshed to true
			on error
				--
			end try
		end tell
	on error
		#  if not yet open at all
		tell application "FMPerception"
			activate
		end tell
		delay 0.4
		tell process "FMPerception"
			set frontmost to true
			#  temp code if not yet registered to get rid of dialog box
			set window_name to name of window 1
			if window_name = "FMPerception Registration" then
				--  close window button
				click button 3 of window 1
				delay 0.2
			end if
			#  end temp code if not yet registered to get rid of dialog box
		end tell
	end try
	if not refreshed then
		try
			tell process "FMPerception"
				#  File:New DDR Browser
				click menu item 1 of menu 1 of menu bar item 3 of menu bar 1
				repeat until window "Untitled" exists
					# added 1.2
					--delay 0.5
				end repeat
				delay 0.2
				#  loop over file list till we get to Summary.xml
				set y to 1
				repeat with this_row in row of outline 1 of scroll area 1 of splitter group 1 of splitter group 1 of sheet 1 of window 1
					if y = 1 then
						#  skip because row 1 is the header and not values
						set y to 0
					else if value of text field 1 of UI element 1 of this_row = "Summary.xml" then
						select this_row
						# Open
						click button 3 of sheet 1 of window 1
						exit repeat
					end if
				end repeat
				set frontmost to true
			end tell
		end try
	end if
end tell
#  optional debug, return true if windows was refreshed else false
refreshed
